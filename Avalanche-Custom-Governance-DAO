// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.7.0 <0.9.0;
import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/IERC20.sol"; // this import adds the IERC20 tken functionality

contract SAQVenture {

    enum VotingOptions { Yes, No }
    enum Status { Accepted, Rejected, Pending }
     struct Proposal {
        uint256 id;
        address author;
        string name;
        uint256 createdAt;
        uint256 votesForYes;
        uint256 votesForNo;
        Status status;
    }
    
    // store all proposals
    mapping(uint => Proposal) public proposals;
    // who already votes for who and to avoid vote twice
    mapping(address => mapping(uint => bool)) public votes;
    // one share for governance tokens
    mapping(address => uint256) public shares;
    uint public totalShares;
    // the IERC20 allow us to use MOJO as our governance token.
    IERC20 public token;
    // the user need minimum 100 MOJO to create a proposal.
    uint constant CREATE_PROPOSAL_MIN_SHARE = 100 * 10 ** 18;
    uint constant VOTING_PERIOD = 7 days;
    uint public nextProposalId;

    // This constructor determines what token will be used for the transactions. MOJO is one of my custom contracts.
    constructor() {
    token = IERC20(0x2b7A4a839aB21bBd8bf158F52980a1e09024E9D2); // MOJO address
    }

    //Deposit MOJO as collateral
    function deposit(uint _amount) external {
        shares[msg.sender] += _amount;
        totalShares += _amount;
        token.transferFrom(msg.sender, address(this), _amount);
    }

    //Withdraw MOJO at conclusion
    function withdraw(uint _amount) external {
        require(shares[msg.sender] >= _amount, 'Not enough shares');
        shares[msg.sender] -= _amount;
        totalShares -= _amount;
        token.transfer(msg.sender, _amount);
    }

    // Proposal management module
    function createProposal(string memory name) external {
    // validate the user has enough shares to create a proposal
    require(shares[msg.sender] >= CREATE_PROPOSAL_MIN_SHARE, 'Not enough shares to create a proposal');
    
    proposals[nextProposalId] = Proposal(
        nextProposalId,
        msg.sender,
        name,
        block.timestamp,
        0,
        0,
        Status.Pending
        );
        nextProposalId++;
    }

    // Voting module - validate that the user has not voted already and the vote period is currently open.
    // validate if the proposal has more than fifty percent of votes in one option we need to change the proposal status to Accepted or Rejected.
    function vote(uint _proposalId, VotingOptions _vote) external {
    Proposal storage proposal = proposals[_proposalId];
    require(votes[msg.sender][_proposalId] == false, 'already voted');
    require(block.timestamp <= proposal.createdAt + VOTING_PERIOD, 'Voting period is over');
    votes[msg.sender][_proposalId] = true;
        if(_vote == VotingOptions.Yes) {
            proposal.votesForYes += shares[msg.sender];
            if(proposal.votesForYes * 100 / totalShares > 50) {
            proposal.status = Status.Accepted;
            }
        } else {
            proposal.votesForNo += shares[msg.sender];
            if(proposal.votesForNo * 100 / totalShares > 50) {
                proposal.status = Status.Rejected;
            }
        }
    }

}
